---
layout: post
title: Training program in F#
tags: F#
published: true
---

I've finally managed to finish the program. It works as a basic, standard STV calculator.
It calculates aggregated ballot list and then in several steps it removes winners and/or losers.
<!-- more -->
In spreadsheet it looks like that:

|       |    |      |
|-------|----|------|
| quota | 20 |      |
|       |    |      |
| step 1|    |      |
| 16    | 24 | 17   |
| A     | A  | D    |
| B     | C  | A    |
| C     | B  | B    |
| D     | D  | C    |
|       |    |      |
| step 2|    |      |
| 8     | 12 | 17   |
| B     | C  | D    |
| C     | B  | B    |
| D     | D  | C    |
|       |    |      |
| step 3|    |      |
| 8     | 12 | 17   |
| C     | C  | D    |
| D     | D  | C    |
|       |    |      |
|       |    | 17   |
| D     | D  | D    |


```F#
module Multidata.Stv.StvCalculator

open StvModels
open Multidata.Stv.StvQuota

let aggregateVotes (voteList: Ballot list) =
    List.countBy (id) voteList 
     |> List.map (fun (b, c) -> {ballot = b; numberOfVotes = c})

let aggregateByCandidate (firstRow: (int * int) list) = 
    firstRow
    |> List.groupBy (fun (a, b) -> a) // aggregate by candidateId
    |> List.map (fun (key , values) -> (key, List.sumBy snd values)) // sum votes for candidate - I don't like: List.sumBy snd values

let getWinner quota firstRow =
    firstRow 
    |> List.tryFind(fun x -> snd x >= quota)

let calculateSurplusVotesToAdd votesForTheNextPreference totalWinnerVotes surplusVotes = 
    (int)(((float)votesForTheNextPreference / (float)totalWinnerVotes) * (float)surplusVotes)

let removeCandidateFromBallot votesSurplus winner aggregatedVote = 
    let candidateId = fst winner
    let newVotes = 
        if aggregatedVote.ballot.[0] = candidateId then
            calculateSurplusVotesToAdd aggregatedVote.numberOfVotes (snd winner) votesSurplus
        else
            aggregatedVote.numberOfVotes
    {
        numberOfVotes = newVotes
        ballot = List.filter (fun x -> x <> candidateId) aggregatedVote.ballot
    }

let removeCandidate aggregatedVotes quota winner = 
    let votesSurplus = (snd winner) - quota
    aggregatedVotes |> List.map(fun x -> removeCandidateFromBallot votesSurplus winner x)

let getLooser firstRow =
    firstRow |> List.minBy(snd)

let getFirstRow aggregatedVotes = 
    aggregatedVotes |> List.map(fun x -> (x.ballot.[0], x.numberOfVotes))

let rec iterationLoop numberOfSeats allWinners droopQuota aggregatedVotes =
    printfn "aggregatedVotes %A" aggregatedVotes
    let firstRow = getFirstRow aggregatedVotes
    let fistRowSum = aggregateByCandidate firstRow
    let maybeWinner = getWinner droopQuota fistRowSum
    match maybeWinner with
    | Some winner ->
        let allWinners = (fst winner) :: allWinners
        if 
            allWinners.Length >= numberOfSeats
        then 
            allWinners
        else
            let aggregatedVotes = removeCandidate aggregatedVotes droopQuota winner
            iterationLoop numberOfSeats allWinners droopQuota aggregatedVotes
    | None ->
        let looser = getLooser fistRowSum
        let aggregatedVotes = removeCandidate aggregatedVotes 0 looser
        iterationLoop numberOfSeats allWinners droopQuota aggregatedVotes

// Only valid and sorted ballots
let mainCalculation (poll: Poll) (voteList: Ballot list) =
    let totalValidPoll = List.length voteList
    let droopQuota = calculateDroopQuota poll.numberOfSeats totalValidPoll

    let aggregatedVotes = aggregateVotes voteList
    let winnersCandidateIdList = iterationLoop poll.numberOfSeats [] droopQuota aggregatedVotes
    poll.candidates |> List.filter(fun x -> List.contains x.candidateId winnersCandidateIdList)

```

* *aggregateVotes* group together the same ballots, and sum the votes for each ballot type. *List.countBy (id) voteList* makes sth. useful. It aggregate list items by the function. In this case it is *fun x -> x* so in short: *id* and that function produces tuple with the key and number of items with identical key. Then it maps to *AggregatedVote* -- note that not type casting is required -- it is done by F#.

* *getFirstRow* the aggregates ballots consists of numberOfVotes and list of preferences. That function gets that numberOfVotes and first preferences.

* *aggregateByCandidate* as in the same candidate might be on the first place in several ballots we need aggregate the result of *getFirstRow* to have sum votes for each candidate.

* *getWinner* that function verify and returns if at least candidate has required quota

* *getLooser* that is similar to getWinner, but it returns the last candidate in first row

* *removeCandidate* removes the candidate from the aggregatedVotes and adds votes to other ballots. That function is used for the winner and for the looser as well.

* *iterationLoop* is a main calculation loop in each step it removes winner or loser and in each steps it verify the number of winners -- as a recursion end condition.

